desc:sd_channelstrip

slider1:hpFreq=20<20, 1050, 1>HP Freq (Hz)
slider2:hpQ=1<0.1, 7, 0.01>HP Q

slider4:lowFreq=200<30,450,1>Low Freq (Hz)
slider5:lowQ=1<0.1, 7, 0.01>Low Q
slider6:lowGain=0<-15, 15, 0.01>Low Gain (dB)

slider8:outputGainDb=0<-15, 15, 0.01>Output Gain (dB)

@init

channels = 2; // FIXME can we make use of num_ch?

function createStereoBuffer(offset)
  (
    this.y = offset + 0;
    this.y1 = offset + channels;
    this.y2 = offset + channels * 2;
    this.x = offset + channels * 3;
    this.x1 = offset + channels * 4;
    this.x2 = offset + channels * 5;
  );

function setHpCoefs()
  local(omega sinOmega cosOmega alpha)
  (
    omega = 2 * $pi * hpFreq / srate;
    sinOmega = sin(omega);
    cosOmega = cos(omega);
    alpha = sinOmega / (2 * hpQ);

    this.b0 = (1 + cosOmega) / 2;
    this.b1 = - (1 + cosOmega);
    this.b2 = (1 + cosOmega) / 2;
    this.a0 = 1 + alpha;
    this.a1 = -2 * cosOmega;
    this.a2 = 1 - alpha;
  );

function setLowCoefs()
  local(A, omega, sinOmega, cosOmega, alpha)
  (
    A = 10 ^ (lowGain / 40);
    omega = 2 * $pi * lowFreq / srate;
    sinOmega = sin(omega);
    cosOmega = cos(omega);
    alpha = sinOmega / (2 * lowQ);

    this.b0 = 1 + alpha * A;
    this.b1 = -2 * cosOmega;
    this.b2 = 1 - alpha * A;
    this.a0 = 1 + alpha / A;
    this.a1 = -2 * cosOmega;
    this.a2 = 1 - alpha / A;
  );

function processSample(
  prevValue, ch,
  x, x1, x2,
  y, y1, y2,
  a0, a1, a2,
  b0, b1, b2
)
  (
    y2[ch] = y1[ch];
    y1[ch] = y[ch];
    y[ch] = (b0 / a0) * x[ch]
      + (b1 / a0) * x1[ch]
      + (b2 / a0) * x2[ch]
      - (a1 / a0) * y1[ch]
      - (a2 / a0) * y2[ch];

    x2[ch] = x1[ch];
    x1[ch] = x[ch];
    x[ch] = prevValue;

    y[ch];
  );

hpBuffer.createStereoBuffer(0);
lowBuffer.createStereoBuffer(256);

hpCoefs.setHpCoefs();
lowCoefs.setLowCoefs();

@slider

hpCoefs.setHpCoefs();
lowCoefs.setLowCoefs();

outputGain = 10 ^ ( outputGainDb / 20 );

@sample

ch = -1;

loop(2,
ch += 1;

    value = spl(ch);

    // High Pass
    value = processSample(
      value, ch,
      hpBuffer.x, hpBuffer.x1, hpBuffer.x2,
      hpBuffer.y, hpBuffer.y1, hpBuffer.y2,
      hpCoefs.a0, hpCoefs.a1, hpCoefs.a2,
      hpCoefs.b0, hpCoefs.b1, hpCoefs.b2
    );

    // Low Band
    lowGain != 0 ? (
      value = processSample(
        value, ch,
        lowBuffer.x, lowBuffer.x1, lowBuffer.x2,
        lowBuffer.y, lowBuffer.y1, lowBuffer.y2,
        lowCoefs.a0, lowCoefs.a1, lowCoefs.a2,
        lowCoefs.b0, lowCoefs.b1, lowCoefs.b2
      );
    );

    spl(ch) = value * outputGain;
);

